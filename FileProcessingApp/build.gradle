// Adds build scan functionality
plugins {
    id 'com.gradle.build-scan' version '1.5'
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}
// End build scan functionality

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'project-report'

group = 'com.atanasg'
version = '0.1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Setting compiler arguments
tasks.withType(JavaCompile) {
    options.encoding = 'utf-8'
    options.compilerArgs = ['-implicit:none', '-Xlint:-options', '-Xlint:unchecked']
}

jar {
    manifest {
        attributes 'Implementation-Title': 'File Processing Application',
                   'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.google.guava:guava:18.0"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.0.31-beta"
}

// Settings for FindBugs tasks and report
tasks.withType(FindBugs) {
    ignoreFailures = true
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet resources.text.fromFile('config/xsl/default.xsl')
    }
}
